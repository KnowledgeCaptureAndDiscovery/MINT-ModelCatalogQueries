#+ endpoint: http://ontosoft.isi.edu:3030/ds/query
#+ method: GET
#+ summary: Query that returns all existing configurations, the model and model version they belong to, their label and description associated to them.

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mc: <https://w3id.org/mint/modelCatalog#>
PREFIX dc: <http://purl.org/dc/terms/>

SELECT ?label ?desc (GROUP_CONCAT(DISTINCT ?input;SEPARATOR=', ') AS ?input_variables) (GROUP_CONCAT(DISTINCT ?output;SEPARATOR=', ') AS ?output_variables) (GROUP_CONCAT(DISTINCT ?module;SEPARATOR=', ') AS ?modules) (GROUP_CONCAT(DISTINCT ?process;SEPARATOR=', ') AS ?processes) ?cag ?tIUnits ?tIValue
WHERE {
  ?_modelConfig_iri a mc:ModelConfiguration;
    rdfs:label ?label.
  OPTIONAL{?_modelConfig_iri dc:description ?desc}.
  OPTIONAL{?_modelConfig_iri mc:hasInput ?input}.
  OPTIONAL{?_modelConfig_iri mc:hasOutput ?output}.
  OPTIONAL{?_modelConfig_iri mc:hasModule ?module}.
  OPTIONAL{?_modelConfig_iri mc:hasProcess ?process}.
  OPTIONAL{?_modelConfig_iri mc:hasCAG ?cag}.
  OPTIONAL{
    ?_modelConfig_iri mc:hasTimeInterval ?ti.
    ?ti mc:usesUnit ?tIUnits;
        mc:hasValue ?tIValue.
  }.
}GROUP BY ?label ?desc ?cag ?tIUnits ?tIValue