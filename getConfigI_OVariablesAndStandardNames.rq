#+ method: GET
#+ summary: Given a model configuration URI, this query returns the inputs/outputs and their data types, corresponding variable presentations, standard names and units.
#+ defaults:
#+   - config: https://w3id.org/mint/instance#pihm_cfg_simple2

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX model: <https://w3id.org/mint/modelCatalog#>

SELECT  DISTINCT  ?prop ?io ?type ?vp ?st ?units
WHERE {
  {
    ?_config_iri model:hasInput ?io.
  }UNION
  {
    ?_config_iri model:hasOutput ?io.
  }
  ?_config_iri ?prop ?io.
  ?io rdf:type ?type.
  FILTER(?type != model:DatasetSpecification).
  ?io model:hasPresentation ?vp.
  OPTIONAL {?vp model:hasStandardVariable ?st.}
  OPTIONAL {?vp model:usesUnit ?units.}
}