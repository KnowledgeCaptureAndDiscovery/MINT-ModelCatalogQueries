#+ method: GET
#+ summary: Given a model configuration URI, this query returns the inputs/outputs and their data types, corresponding variable presentations, standard names and units.
#+ defaults:
#+   - config: https://w3id.org/mint/instance/pihm

PREFIX model: <https://w3id.org/mint/modelCatalog#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT  DISTINCT  ?prop ?io ?iolabel ?format ?type ?dim ?vp ?st ?units
WHERE {
  {
    ?_config_iri model:hasInput ?io.
  }UNION
  {
    ?_config_iri model:hasOutput ?io.
  }
  ?_config_iri ?prop ?io.
  OPTIONAL {?io model:hasFormat ?format}
  OPTIONAL {?io model:hasDimensionality ?dim}
  ?io a ?type.
  ?io rdfs:label ?iolabel.
  FILTER(?type != model:DatasetSpecification).
  FILTER(?type != model:VariablePresentation ).  
  OPTIONAL {?io model:hasPresentation ?vp.?vp model:hasStandardVariable ?st.}
  OPTIONAL {?io model:hasPresentation ?vp.?vp model:usesUnit ?units.}
}
